#compdef burp
# ------------------------------------------------------------------------------
# Copyright (c) 2011 Github zsh-users - http://github.com/zsh-users
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#     * Redistributions of source code must retain the above copyright
#       notice, this list of conditions and the following disclaimer.
#     * Redistributions in binary form must reproduce the above copyright
#       notice, this list of conditions and the following disclaimer in the
#       documentation and/or other materials provided with the distribution.
#     * Neither the name of the zsh-users nor the
#       names of its contributors may be used to endorse or promote products
#       derived from this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
# ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
# WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL ZSH-USERS BE LIABLE FOR ANY
# DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
# (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
# LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
# ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
# SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
# ------------------------------------------------------------------------------
# Description
# -----------
#
#  Completion script for burp (https://github.com/falconindy/burp).
#
# ------------------------------------------------------------------------------
# Authors
# -------
#
#  * Julien Nicoulaud <julien.nicoulaud@gmail.com>
#
# ------------------------------------------------------------------------------
# -*- mode: zsh; sh-indentation: 2; indent-tabs-mode: nil; sh-basic-offset: 2; -*-
# vim: ft=zsh sw=2 ts=2 et
# ------------------------------------------------------------------------------


typeset -A opt_args
local context state line curcontext="$curcontext"

_burp() {
  _arguments \
    '(- : *)'{-h,--help}"[show help]" \
    '(-u --user)'{-u,--user}"[AUR login username]: :_users" \
    '(-p --password)'{-p,--password}"[AUR login password]:password" \
    '(-c --cat)'{-c,--cat}"[assign the uploaded package with category]: :_burp_categories" \
    '(-C --cookies)'{-C,--cookies}"[file used to store cookies rather than the default temporary file]: :_files" \
    '(-k --keep-cookies)'{-k,--keep-cookies}"[cookies will be persistent and reused for logins]" \
    '(-v --verbose)*'{-v,--verbose}"[be more verbose, pass twice for debug info]" \
    ':source package:_files -g \*.src.tar.gz'
}

(( $+functions[_burp_categories] )) ||
_burp_categories() {
  local categories; categories=(${${${(f)"$(_call_program categories $service --cat help 2>/dev/null)"}:#*categories*}//[[:space:]]/})
  categories+=('help:show available categories')
  _describe -t categories 'category' categories "$@"
}

_burp "$@"
